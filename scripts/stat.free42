@ Statistics utility for the Swissmicros DM42
@ Uses some free42 functions, so it will not work on the original 42s

@@@ FEATURES @@@
@ - Min, Max
@ - Median
@ - Mean
@ - Standard Deviation
@ - Q1, Q3
@ - Range
@ - Interquartile Range
@ - Sum
@ - Outlier fences


@ Matrix sorting routine
00 { 46-Byte Prgm }
01 LBL "SORT"
02 STO "A"
03 INDEX "A"
04 1
05 1
06 STOIJ
07 DROPN 2
08 LBL 00
09 [MIN]
10 DROP
11 RCLIJ
12 DROP
13 R<>R
14 DROPN 2
15 I+
16 FC? 76
17 GTO 00
18 RCL "A"
19 END


@ Main program
00 { 395-Byte Prgm }
01 LBL "STAT"
@ Sort matix and store in A, leave original in X
02 XEQ "SORT"
03 DROP

@ Menu page 1
04 LBL 01
05 CLMENU
06 "MIN"
07 KEY 1 GTO 10
08 "MAX"
09 KEY 2 GTO 11
10 "MEAD"
11 KEY 3 GTO 14
12 "MEAN"
13 KEY 4 GTO 12
14 "STD"
15 KEY 5 GTO 19
16 "OUTLR"
17 KEY 6 GTO 20
18 KEY 8 GTO 02 @ Next Page
19 KEY 9 GTO 09 @ Exit to custom menu
20 MENU
21 STOP
22 RTN

@ Meny page 2
23 LBL 02
24 CLMENU
25 "Q1"
26 KEY 1 GTO 16
27 "Q3"
28 KEY 2 GTO 17
29 "RANGE"
30 KEY 3 GTO 15
31 "IQR "
32 KEY 4 GTO 18
33 "TSAM"
34 KEY 5 GTO 08
35 "SUM"
36 KEY 6 GTO 13
37 KEY 7 GTO 01 @ Previous Page
38 KEY 9 GTO 09 @ Exit to custom menu
39 MENU
40 STOP
41 RTN

@ Toggle flag 05
@ Unset - Sample
@ Set - Population
@ Currently just used in standard deviation
42 LBL 08
43 FS? 05
44 GTO 35
45 SF 05
46 RTN

@ Clears flag 05
47 LBL 35
48 CF 05
49 RTN

@ Exits to custom menu
50 LBL 09
51 SF 27
52 RTN

@ Min
53 LBL 10
54 XEQ 36 @ Reset indices
55 [MIN]
@ Drop Index
56 X<>Y
57 DROP
58 RTN

@ Max
59 LBL 11
60 XEQ 36 @ Reset indices
61 [MAX]
@ Drop Index
62 X<>Y
63 DROP
64 RTN

@ Sum
65 LBL 13
66 XEQ 36 @ Reset indices
67 0
68 LBL 30
69 RCLEL
70 +
71 I+
72 FC? 76 @ Has index not overflowed mat len?
73 GTO 30
74 RTN

@ Mean
75 LBL 12
76 XEQ 13 @ Sum
77 XEQ 37 @ Data Length
78 ÷
79 RTN

@ Median
80 LBL 14
81 XEQ 37 @ Data Length
@ (n+1)/2
82 1
83 +
84 2
85 ÷
86 XEQ 31 @ Decimel index
87 RTN

@ Range
88 LBL 15
@ Max - Min
89 XEQ 11
90 XEQ 10
91 -
92 RTN

@ Q1
93 LBL 16
94 XEQ 37
@ (n+1)/4
95 1
96 +
97 4
98 ÷
99 XEQ 31 @ Decimel index
100 RTN

@ Q3
101 LBL 17
102 XEQ 37
@ 3(n+1)/4
103 1
104 +
105 4
106 ÷
107 3
108 ×
109 XEQ 31 @ Decimel index
110 RTN

@ IQN
111 LBL 18
@ Q3 - Q1
112 XEQ 17
113 XEQ 16
114 -
115 RTN

@ Standard Deviation
116 LBL 19
@ Store unmodified from A data in B
@ Also put data on the stack
117 RCL "A"
118 STO "B"
@ Squared difference from mean
119 XEQ 12 @ Mean
120 -
121 X↑2
@ Sum
122 STO "A"
123 XEQ 13
@ Uses flag 05 to determine sample or population
@ For population, devide by n
@ For sample, devide by n-1
124 XEQ 37
125 XEQ 34
126 ÷
@ Square root
127 SQRT
@ Drop data matrix from stack
128 X<>Y
129 DROP
@ Put clean data back in A
130 RCL "B"
131 STO "A"
132 DROP
133 RTN

@ Outlier fences
@ >Q3 + 1.5(IQN)
@ <Q1 - 1.5(IQN)
134 LBL 20
135 XEQ 18 @ IQN
136 1.5
137 ×
138 DUP
@ Upper Fence
139 XEQ 17 @ Q3
140 +
141 X<>Y
@ Lower Fence
142 XEQ 16 @ Q1
143 X<>Y
144 -
145 RTN

@ Standard deviation internals
@ Used to subtract 1 from n for sample mode
146 LBL 34
147 FS? 05
148 RTN
149 1
150 -
151 RTN

@ Decimal index
@ Used for median, Q1, Q3
@ Will index into data with index provided by X
@ If X is a decimal, will return the average of the two values surrounding the index, not weighted
152 LBL 31
153 DUP
154 FP
155 X≠0? @ If float portion is non-zero jmp 32
156 GTO 32
157 DROP @ Drop float portion
158 XEQ 33 @ Index
159 RTN

@ Decimal index internals
@ If float portion of index is non-zero
160 LBL 32
161 DROP @ Drop float portion
162 IP @ Take integer portion
163 DUP
@ Index(X) + Index(X+1) / 2
164 1
165 +
166 XEQ 33 @ Index
167 X<>Y
168 XEQ 33 @ Index
169 +
170 2
171 ÷
172 RTN

@ Index into data
@ Does not work with decimals
173 LBL 33
174 INDEX "A"
175 1
176 STOIJ @ X Val provided by caller
177 DROPN 2
178 RCLEL @ Index
179 RTN

@ Reset matrix indices to 1, 1
180 LBL 36
181 INDEX "A"
182 1
183 1
184 STOIJ
185 DROPN 2
186 RTN

@ Gets length of data
187 LBL 37
188 RCL "A"
189 DIM?
190 DROP @ Drop width
191 RTN

192 END
